Introduction{{{Templates for generating UNIX \Mono{ar} archives, which are conveniently text-only.}}}
utils_lib : From lib:utils

{{{A BSD \Mono{ar} style archive}}}
archive_tmpl : Template {
	{{{The collection of \{file_tmpl} files.}}}
	args : Required
	value : For file : args Reduce acc & file.value With acc : "!<arch>\n"
	{{{The default user ID to create files.}}}
	uid : 0
	{{{The default group ID to create files.}}}
	gid : 0
	{{{The default modification time, as a UNIX epoch time.}}}
	mtime : 0
	{{{The user permission bits.}}}
	user : 6
	{{{The group permission bits.}}}
	group : 4
	{{{The other permission bits.}}}
	other : 4
}

{{{A file in a compressed archive}}}
file_tmpl : Template {
	{{{The file name.}}}
	file_name : Required
	{{{The file contents.}}}
	contents : Required
	uid : Used
	gid : Used
	mtime : Used
	user : Used
	group : Used
	other : Used

	file_name_length : utils_lib.str_utf8_length(file_name)
	# If the file name is more than 16 characters, use the BSD `ar` encoding.
	file_header : If file_name_length > 16 Then "#1/" & file_name_length Else file_name
	header_suffix : If file_name_length > 16 Then file_name Else ""
	header_perms :
		utils_lib.str_pad(uid, length : 6) &
		utils_lib.str_pad(gid, length : 6) &
		utils_lib.str_pad(user & group & other, length : 8)
	value :
		utils_lib.str_pad(file_header, length : 16) &
		utils_lib.str_pad(mtime, length : 12) &
		header_perms &
		utils_lib.str_pad(utils_lib.str_utf8_length(contents, header_suffix), length : 10) &
		"`\n" & header_suffix & contents
}
